import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java-library'
apply plugin: 'java'

libsDirName = System.getenv("libsDirName") == null ? libsDirName : System.getenv("libsDirName")
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

// First number indicates breaking changes (removals etc.)
// Second number indicates non-breaking changes (new libraries etc.)
// Third number indicates bug fixes or small updates
version = "1.1.0-RELEASE"

sourceSets {
    main {
        java {
            srcDirs 'src'
        }

        resources {
            srcDirs 'resources'
        }
    }
    
    test {
    	java {
            srcDirs 'test'
        }
    }
}

repositories {
	mavenCentral()
	
    flatDir {
        dirs 'libs'
    }
    
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
	
	maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    
    maven {
    	url 'https://m2.dv8tion.net/releases'
    }

    maven {
        url 'https://jitpack.io'
    }

    maven {
        url 'https://repo.varoplugin.de/repository/maven-public/'
    }

    maven {
        url 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

configurations {
    internalLibs
    implementation.extendsFrom(internalLibs)
}

dependencies {
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.2")

    // Internal Libraries
    this.addModularInternal('com.googlecode.json-simple:json-simple:1.1.1', 'gson', false)
    this.addModularInternal('net.dv8tion:JDA:5.0.0-alpha.4', 'jda', false)
	this.addModularInternal('com.github.CuukyOfficial:CFW:master-SNAPSHOT', 'CFW', true)
    this.addModularInternal('com.github.almighty-satan:jo2sql:1.2.2', 'jo2sql', true)
    this.addModularInternal('net.kyori:adventure-text-minimessage:4.11.0', 'minimessages', false)

    // Velocity + Spigot Support
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
    this.addModularCompile("com.velocitypowered:velocity-api:3.0.1", "velocity", true)
    this.addModularCompile('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT', 'spigot', true)
}

boolean checkLib(String filePath) {
    return file('libs/' + filePath + '.jar').exists()
}

void addModularCompile(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.implementation name: fileName
    else
        this.dependencies.implementation (repoName) { changing = changingB }
}

void addModularInternal(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.internalLibs name: fileName
    else
        this.dependencies.internalLibs (repoName) { changing = changingB }
}

jar {
    manifest {
        attributes(
                'Manifest-Version': version,
                'Class-Path': '.',
                'Main-Class': 'net.kettlemc.plugin.MainLauncher',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.internalLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from (sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [name: "LibrariesBukkit"]
        filter ReplaceTokens, tokens: [version: version]
        filter ReplaceTokens, tokens: [author: "KettleMC"]
        filter ReplaceTokens, tokens: [main: "net.kettlemc.libraries.Libraries"]
    }
}

test {
	useJUnitPlatform()

	testLogging {
        exceptionFormat = 'full'
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}
